# Copyright (c) Meta Platforms, Inc. and affiliates.
#
# This source code is licensed under both the MIT license found in the
# LICENSE-MIT file in the root directory of this source tree and the Apache
# License, Version 2.0 found in the LICENSE-APACHE file in the root directory
# of this source tree.

# Implementation of the Haskell build rules.

load("@prelude//:paths.bzl", "paths")
load("@prelude//cxx:archive.bzl", "make_archive")
load(
    "@prelude//cxx:cxx.bzl",
    "get_auto_link_group_specs",
)
load(
    "@prelude//cxx:cxx_context.bzl",
    "get_cxx_toolchain_info",
)
load(
    "@prelude//cxx:cxx_toolchain_types.bzl",
    "CxxToolchainInfo",
    "LinkerInfo",
    "LinkerType",
    "PicBehavior",
)
load("@prelude//cxx:groups.bzl", "get_dedupped_roots_from_groups")
load(
    "@prelude//cxx:link_groups.bzl",
    "LinkGroupContext",
    "create_link_groups",
    "find_relevant_roots",
    "get_filtered_labels_to_links_map",
    "get_filtered_links",
    "get_link_group_info",
    "get_link_group_preferred_linkage",
    "get_public_link_group_nodes",
    "get_transitive_deps_matching_labels",
    "is_link_group_shlib",
)
load(
    "@prelude//cxx:linker.bzl",
    "LINKERS",
    "get_rpath_origin",
    "get_shared_library_flags",
)
load(
    "@prelude//cxx:preprocessor.bzl",
    "CPreprocessor",
    "CPreprocessorArgs",
    "cxx_inherited_preprocessor_infos",
    "cxx_merge_cpreprocessors",
)
load(
    "@prelude//linking:link_groups.bzl",
    "gather_link_group_libs",
    "merge_link_group_lib_info",
)
load(
    "@prelude//linking:link_info.bzl",
    "Archive",
    "ArchiveLinkable",
    "LibOutputStyle",
    "LinkArgs",
    "LinkInfo",
    "LinkInfos",
    "LinkStyle",
    "LinkedObject",
    "MergedLinkInfo",
    "SharedLibLinkable",
    "append_linkable_args",
    "create_merged_link_info",
    "default_output_style_for_link_strategy",
    "get_lib_output_style",
    "get_link_args_for_strategy",
    "get_output_styles_for_linkage",
    "legacy_output_style_to_link_style",
    "map_to_link_infos",
    "to_link_strategy",
    "unpack_link_args",
)
load(
    "@prelude//linking:linkable_graph.bzl",
    "LinkableGraph",
    "create_linkable_graph",
    "create_linkable_graph_node",
    "create_linkable_node",
    "get_linkable_graph_node_map_func",
)
load(
    "@prelude//linking:linkables.bzl",
    "linkables",
)
load(
    "@prelude//linking:shared_libraries.bzl",
    "SharedLibraryInfo",
    "create_shared_libraries",
    "create_shlib_symlink_tree",
    "merge_shared_libraries",
    "traverse_shared_library_info",
)
load("@prelude//linking:types.bzl", "Linkage")
load(
    "@prelude//python:python.bzl",
    "PythonLibraryInfo",
)
load("@prelude//utils:argfile.bzl", "at_argfile")
load("@prelude//utils:arglike.bzl", "ArgLike")
load("@prelude//utils:set.bzl", "set")
load("@prelude//utils:utils.bzl", "filter_and_map_idx", "flatten")
load(
    ":compile.bzl",
    "CompileResultInfo",
    "compile",
    "target_metadata",
)
load(
    ":haskell_haddock.bzl",
    "haskell_haddock_lib",
)
load(
    ":library_info.bzl",
    "HaskellLibraryInfo",
    "HaskellLibraryInfoTSet",
    "HaskellLibraryProvider",
)
load(
    ":link_info.bzl",
    "HaskellLinkGroupInfo",
    "HaskellLinkInfo",
    "HaskellProfLinkInfo",
    "attr_link_style",
    "cxx_toolchain_link_style",
)
load(":pkg_conf.bzl", "append_pkg_conf_link_fields_for_link_infos")
load(
    ":toolchain.bzl",
    "DynamicHaskellPackageDbInfo",
    "DynamicHaskellToolchainLibraryInfo",
    "HaskellPackageDbTSet",
    "HaskellToolchainInfo",
    "HaskellToolchainLibrary",
)
load(
    ":util.bzl",
    "attr_deps",
    "attr_deps_haskell_lib_infos",
    "attr_deps_haskell_link_group_infos",
    "attr_deps_haskell_link_infos",
    "attr_deps_haskell_link_infos_sans_template_deps",
    "attr_deps_haskell_toolchain_libraries",
    "attr_deps_merged_link_infos",
    "attr_deps_profiling_link_infos",
    "attr_deps_shared_library_infos",
    "error_on_non_haskell_srcs",
    "get_artifact_suffix",
    "get_source_prefixes",
    "is_haskell_boot",
    "is_haskell_src",
    "output_extensions",
    "src_to_module_name",
    "srcs_to_pairs",
    "to_hash",
)

HaskellIndexingTSet = transitive_set()

# A list of hie dirs
HaskellIndexInfo = provider(
    fields = {
        "info": provider_field(typing.Any, default = None),  # dict[LinkStyle, HaskellIndexingTset]
    },
)

# This conversion is non-standard, see TODO about link style below
def _to_lib_output_style(link_style: LinkStyle) -> LibOutputStyle:
    return default_output_style_for_link_strategy(to_link_strategy(link_style))

def _attr_preferred_linkage(ctx: AnalysisContext) -> Linkage:
    preferred_linkage = ctx.attrs.preferred_linkage

    # force_static is deprecated, but it has precedence over preferred_linkage
    if getattr(ctx.attrs, "force_static", False):
        preferred_linkage = "static"

    return Linkage(preferred_linkage)

# --

def _toolchain_target_metadata_impl(
        actions: AnalysisActions,
        haskell_toolchain: HaskellToolchainInfo,
        output: OutputArtifact,
        libname: str,
        pkg_deps: ResolvedDynamicValue,
        md_gen: RunInfo) -> list[Provider]:
    package_db = pkg_deps.providers[DynamicHaskellPackageDbInfo].packages

    md_args = cmd_args(md_gen, "--ghc-pkg", haskell_toolchain.packager, "--package-name", libname, "--output", output)
    if libname in package_db:
        pkg = package_db[libname].reduce("root")
        md_args.add("--package-dir", pkg.db)

    actions.run(
        md_args,
        category = "haskell_toolchain_library_metadata",
        identifier = libname,
    )

    return []

_toolchain_target_metadata = dynamic_actions(
    impl = _toolchain_target_metadata_impl,
    attrs = {
        "haskell_toolchain": dynattrs.value(typing.Any),
        "output": dynattrs.output(),
        "libname": dynattrs.value(typing.Any),
        "pkg_deps": dynattrs.option(dynattrs.dynamic_value()),
        "md_gen": dynattrs.value(typing.Any),
    },
)

def _get_toolchain_haskell_package_id_impl(
        actions: AnalysisActions,
        md_file: ArtifactValue) -> list[Provider]:
    md = md_file.read_json()
    package_id = md["id"]
    return [DynamicHaskellToolchainLibraryInfo(id = package_id)]

_get_toolchain_haskell_package_id = dynamic_actions(
    impl = _get_toolchain_haskell_package_id_impl,
    attrs = {
        "md_file": dynattrs.artifact_value(),
    },
)

def haskell_toolchain_library_impl(ctx: AnalysisContext):
    md_file = ctx.actions.declare_output(ctx.label.name + ".md.json")
    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]
    pkg_deps = haskell_toolchain.packages.dynamic if haskell_toolchain.packages else None
    ctx.actions.dynamic_output_new(
        _toolchain_target_metadata(
            haskell_toolchain = haskell_toolchain,
            output = md_file.as_output(),
            libname = ctx.attrs.name,
            pkg_deps = pkg_deps,
            md_gen = ctx.attrs._generate_toolchain_lib_metadata[RunInfo],
        ),
    )
    dynamic = ctx.actions.dynamic_output_new(
        _get_toolchain_haskell_package_id(md_file = md_file),
    )
    sub_targets = {"metadata": [DefaultInfo(default_output = md_file)]}
    return [
        DefaultInfo(sub_targets = sub_targets),
        HaskellToolchainLibrary(
            name = ctx.attrs.name,
            dynamic = dynamic,
        ),
    ]

# --

def _get_haskell_prebuilt_libs(
        ctx: AnalysisContext,
        link_style: LinkStyle,
        enable_profiling: bool) -> list[Artifact]:
    if link_style == LinkStyle("shared"):
        if enable_profiling:
            # Profiling doesn't support shared libraries
            return []

        return ctx.attrs.shared_libs.values()
    elif link_style == LinkStyle("static"):
        if enable_profiling:
            return ctx.attrs.profiled_static_libs
        return ctx.attrs.static_libs
    elif link_style == LinkStyle("static_pic"):
        if enable_profiling:
            return ctx.attrs.pic_profiled_static_libs
        return ctx.attrs.pic_static_libs
    else:
        fail("Unexpected LinkStyle: " + link_style.value)

def haskell_prebuilt_library_impl(ctx: AnalysisContext) -> list[Provider]:
    # MergedLinkInfo for both with and without profiling
    native_infos = []
    prof_native_infos = []

    haskell_infos = []
    shared_library_infos = []
    for dep in ctx.attrs.deps:
        used = False
        if HaskellLinkInfo in dep:
            used = True
            haskell_infos.append(dep[HaskellLinkInfo])
        li = dep.get(MergedLinkInfo)
        if li != None:
            used = True
            native_infos.append(li)
            if HaskellLinkInfo not in dep:
                prof_native_infos.append(li)
        if HaskellProfLinkInfo in dep:
            prof_native_infos.append(dep[HaskellProfLinkInfo].prof_infos)
        if SharedLibraryInfo in dep:
            used = True
            shared_library_infos.append(dep[SharedLibraryInfo])
        if PythonLibraryInfo in dep:
            used = True
        if not used:
            fail("Unexpected link info encountered")

    hlibinfos = {}
    prof_hlibinfos = {}
    hlinkinfos = {}
    prof_hlinkinfos = {}
    link_infos = {}
    prof_link_infos = {}
    for link_style in LinkStyle:
        libs = _get_haskell_prebuilt_libs(ctx, link_style, False)
        prof_libs = _get_haskell_prebuilt_libs(ctx, link_style, True)

        hlibinfo = HaskellLibraryInfo(
            name = ctx.attrs.name,
            db = ctx.attrs.db,
            empty_db = None,
            deps_db = None,
            objects = {},
            dependencies = [],
            toolchain_dependencies = [],
            import_dirs = {},
            hie_files = {},
            stub_dirs = [],
            id = ctx.attrs.id,
            dynamic = None,
            libs = libs,
            version = ctx.attrs.version,
            is_prebuilt = True,
            profiling_enabled = False,
            md_file = None,
        )
        prof_hlibinfo = HaskellLibraryInfo(
            name = ctx.attrs.name,
            db = ctx.attrs.db,
            empty_db = None,
            deps_db = None,
            objects = {},
            dependencies = [],
            toolchain_dependencies = [],
            import_dirs = {},
            hie_files = {},
            stub_dirs = [],
            id = ctx.attrs.id,
            dynamic = None,
            libs = prof_libs,
            version = ctx.attrs.version,
            is_prebuilt = True,
            profiling_enabled = True,
            md_file = None,
        )

        def archive_linkable(lib):
            return ArchiveLinkable(
                archive = Archive(artifact = lib),
                linker_type = LinkerType("gnu"),
            )

        def shared_linkable(lib):
            return SharedLibLinkable(
                lib = lib,
            )

        linkables = [
            (shared_linkable if link_style == LinkStyle("shared") else archive_linkable)(lib)
            for lib in libs
        ]
        prof_linkables = [
            (shared_linkable if link_style == LinkStyle("shared") else archive_linkable)(lib)
            for lib in prof_libs
        ]

        hlibinfos[link_style] = hlibinfo
        hlinkinfos[link_style] = ctx.actions.tset(
            HaskellLibraryInfoTSet,
            value = hlibinfo,
            children = [lib.info[link_style] for lib in haskell_infos],
        )
        prof_hlibinfos[link_style] = prof_hlibinfo
        prof_hlinkinfos[link_style] = ctx.actions.tset(
            HaskellLibraryInfoTSet,
            value = prof_hlibinfo,
            children = [lib.prof_info[link_style] for lib in haskell_infos],
        )
        link_infos[link_style] = LinkInfos(
            default = LinkInfo(
                pre_flags = ctx.attrs.exported_linker_flags,
                post_flags = ctx.attrs.exported_post_linker_flags,
                linkables = linkables,
            ),
        )
        prof_link_infos[link_style] = LinkInfos(
            default = LinkInfo(
                pre_flags = ctx.attrs.exported_linker_flags,
                post_flags = ctx.attrs.exported_post_linker_flags,
                linkables = prof_linkables,
            ),
        )

    haskell_link_infos = HaskellLinkInfo(
        info = hlinkinfos,
        prof_info = prof_hlinkinfos,
        extra = {},
    )
    haskell_lib_provider = HaskellLibraryProvider(
        lib = hlibinfos,
        prof_lib = prof_hlibinfos,
    )

    # The link info that will be used when this library is a dependency of a non-Haskell
    # target (e.g. a cxx_library()). We need to pick the profiling libs if we're in
    # profiling mode.
    default_link_infos = prof_link_infos if ctx.attrs.enable_profiling else link_infos
    default_native_infos = prof_native_infos if ctx.attrs.enable_profiling else native_infos
    merged_link_info = create_merged_link_info(
        ctx,
        # We don't have access to a CxxToolchain here (yet).
        # Give that it's already built, this doesn't mean much, use a sane default.
        pic_behavior = PicBehavior("supported"),
        link_infos = {_to_lib_output_style(s): v for s, v in default_link_infos.items()},
        exported_deps = default_native_infos,
    )

    prof_merged_link_info = create_merged_link_info(
        ctx,
        # We don't have access to a CxxToolchain here (yet).
        # Give that it's already built, this doesn't mean much, use a sane default.
        pic_behavior = PicBehavior("supported"),
        link_infos = {_to_lib_output_style(s): v for s, v in prof_link_infos.items()},
        exported_deps = prof_native_infos,
    )

    solibs = {}
    for soname, lib in ctx.attrs.shared_libs.items():
        solibs[soname] = LinkedObject(output = lib, unstripped_output = lib)
    shared_libs = create_shared_libraries(ctx, solibs)

    linkable_graph = create_linkable_graph(
        ctx,
        node = create_linkable_graph_node(
            ctx,
            linkable_node = create_linkable_node(
                ctx = ctx,
                exported_deps = ctx.attrs.deps,
                link_infos = {_to_lib_output_style(s): v for s, v in link_infos.items()},
                shared_libs = shared_libs,
                default_soname = None,
            ),
        ),
        deps = ctx.attrs.deps,
    )

    inherited_pp_info = cxx_inherited_preprocessor_infos(ctx.attrs.deps)
    own_pp_info = CPreprocessor(
        args = CPreprocessorArgs(args = flatten([["-isystem", d] for d in ctx.attrs.cxx_header_dirs])),
    )

    return [
        DefaultInfo(),
        haskell_lib_provider,
        cxx_merge_cpreprocessors(ctx, [own_pp_info], inherited_pp_info),
        merge_shared_libraries(
            ctx.actions,
            shared_libs,
            shared_library_infos,
        ),
        merge_link_group_lib_info(deps = ctx.attrs.deps),
        haskell_link_infos,
        merged_link_info,
        HaskellProfLinkInfo(
            prof_infos = prof_merged_link_info,
        ),
        linkable_graph,
    ]

def _register_package_conf(
        actions: AnalysisActions,
        pkg_conf: Artifact,
        db: OutputArtifact,
        registerer: RunInfo,
        packager: RunInfo,
        category_prefix: str,
        artifact_suffix: str,
        use_empty_lib: bool) -> None:
    register_cmd = cmd_args(registerer)
    register_cmd.add("--ghc-pkg", packager)
    register_cmd.add("--output", db)
    register_cmd.add("--package-conf", pkg_conf)

    actions.run(
        register_cmd,
        category = category_prefix + artifact_suffix.replace("-", "_"),
        identifier = "empty" if use_empty_lib else "final",
        # explicit turn this on for local_only actions to upload their results.
        allow_cache_upload = True,
    )

def _mk_artifact_dir(dir_prefix: str, profiled: bool, link_style, subdir: str = "") -> str:
    suffix = get_artifact_suffix(link_style, profiled)
    if subdir:
        suffix = paths.join(suffix, subdir)
    return "\"${pkgroot}/" + dir_prefix + "-" + suffix + "\""

_WritePackageConfOptions = record(
    for_deps = bool,
    profiling = list[bool],
    link_style = LinkStyle,
    # NB: We only expect to need one `LinkInfo` here, but `map_to_link_infos`
    # returns a list, so it may be more convenient to use a list in the future.
    link_infos = list[LinkInfo],
    pkgname = str,
    hlis = list[HaskellLibraryInfo],
    use_empty_lib = bool,
    enable_profiling = bool,
    artifact_suffix = str,
    srcs = list[typing.Any],
    strip_prefix = list[str],
    haskell_toolchain = Provider, #HaskellToolchainInfo,
    registerer = RunInfo,
)

def _write_package_conf_impl(
        actions: AnalysisActions,
        md_file: ArtifactValue,
        toolchain_lib_dyn_infos: list[ResolvedDynamicValue],
        pkg_conf: OutputArtifact,
        db: OutputArtifact,
        libname: str | None,
        arg: _WritePackageConfOptions) -> list[Provider]:
    md = md_file.read_json()
    module_map = md["module_mapping"]

    source_prefixes = get_source_prefixes(arg.srcs, module_map)
    source_prefixes_excluded = [prefix for prefix in source_prefixes if prefix not in arg.strip_prefix]

    modules = [
        module
        for module in md["module_graph"].keys()
        if not module.endswith("-boot")
    ]

    # XXX use a single import dir when this package db is used for resolving dependencies with ghc -M,
    #     which works around an issue with multiple import dirs resulting in GHC trying to locate interface files
    #     for each exposed module
    if arg.for_deps:
        import_dirs = ["."]
    elif not source_prefixes_excluded:
        import_dirs = [
            _mk_artifact_dir("mod", profiled, arg.link_style)
            for profiled in arg.profiling
        ]
    else:
        import_dirs = [
            _mk_artifact_dir("mod", profiled, arg.link_style, src_prefix)
            for profiled in arg.profiling
            for src_prefix in source_prefixes_excluded
        ]

    toolchain_lib_ids = [info.providers[DynamicHaskellToolchainLibraryInfo].id for info in toolchain_lib_dyn_infos]

    conf = cmd_args(
        "name: " + arg.pkgname,
        "version: 1.0.0",
        "id: " + arg.pkgname,
        "key: " + arg.pkgname,
        "exposed: False",
        "exposed-modules: " + ", ".join(modules),
        "import-dirs:" + ", ".join(import_dirs),
        "depends: " + ", ".join(toolchain_lib_ids + [lib.id for lib in arg.hlis]),
    )

    if not arg.use_empty_lib:
        if not libname:
            fail("argument `libname` cannot be empty, when use_empty_lib == False")

        if arg.enable_profiling:
            # Add the `-p` suffix otherwise ghc will look for objects
            # following this logic (https://fburl.com/code/3gmobm5x) and will fail.
            libname += "_p"

        if arg.link_style == LinkStyle("shared"):
            library_dirs = [_mk_artifact_dir("lib", profiled, arg.link_style) for profiled in arg.profiling]
        else:
            library_dirs = [_mk_artifact_dir("lib", profiled, link_style) for profiled in arg.profiling for link_style in [arg.link_style, LinkStyle("shared")]]

        conf.add(cmd_args(cmd_args(library_dirs, delimiter = ","), format = "library-dirs: {}"))
        conf.add(cmd_args(libname, format = "hs-libraries: {}"))

    append_pkg_conf_link_fields_for_link_infos(
        pkgname = arg.pkgname,
        pkg_conf = conf,
        link_infos = arg.link_infos,
    )

    pkg_conf_artifact = actions.write(pkg_conf, conf)

    _register_package_conf(
        actions,
        pkg_conf_artifact,
        db,
        arg.registerer,
        arg.haskell_toolchain.packager,
        "haskell_package_",
        arg.artifact_suffix,
        arg.use_empty_lib,
    )

    return []

_write_package_conf = dynamic_actions(
    impl = _write_package_conf_impl,
    attrs = {
        "md_file": dynattrs.artifact_value(),
        "toolchain_lib_dyn_infos": dynattrs.list(dynattrs.dynamic_value()),
        "pkg_conf": dynattrs.output(),
        "db": dynattrs.output(),
        "libname": dynattrs.value(typing.Any),
        "arg": dynattrs.value(typing.Any),
    },
)

# Create a package
#
# The way we use packages is a bit strange. We're not using them
# at link time at all: all the linking info is in the
# HaskellLibraryInfo and we construct linker command lines
# manually. Packages are used for:
#
#  - finding .hi files at compile time
#
#  - symbol namespacing (so that modules with the same name in
#    different libraries don't clash).
#
#  - controlling module visibility: only dependencies that are
#    directly declared as dependencies may be used
#
#  - by GHCi when loading packages into the repl
#
#  - when linking binaries statically, in order to pass libraries
#    to the linker in the correct order
def _make_package(
        ctx: AnalysisContext,
        link_style: LinkStyle,
        pkgname: str,
        libname: str | None,
        hlis: list[HaskellLibraryInfo],
        profiling: list[bool],
        enable_profiling: bool,
        use_empty_lib: bool,
        md_file: Artifact,
        for_deps: bool = False) -> Artifact:
    artifact_suffix = get_artifact_suffix(link_style, enable_profiling)

    if for_deps:
        pkg_conf = ctx.actions.declare_output("pkg-" + artifact_suffix + "_deps.conf")
        db = ctx.actions.declare_output("db-" + artifact_suffix + "_deps", dir = True)
    elif use_empty_lib:
        pkg_conf = ctx.actions.declare_output("pkg-" + artifact_suffix + "_empty.conf")
        db = ctx.actions.declare_output("db-" + artifact_suffix + "_empty", dir = True)
    else:
        pkg_conf = ctx.actions.declare_output("pkg-" + artifact_suffix + ".conf")
        db = ctx.actions.declare_output("db-" + artifact_suffix, dir = True)

    link_infos = map_to_link_infos([
        get_link_args_for_strategy(
            ctx,
            [
                lib[MergedLinkInfo]
                for lib in ctx.attrs.extra_libraries
                if MergedLinkInfo in lib
            ],
            to_link_strategy(link_style),
        ),
    ])

    arg = _WritePackageConfOptions(
        for_deps = for_deps,
        profiling = profiling,
        link_style = link_style,
        link_infos = link_infos,
        pkgname = pkgname,
        hlis = hlis,
        use_empty_lib = use_empty_lib,
        enable_profiling = enable_profiling,
        artifact_suffix = artifact_suffix,
        srcs = ctx.attrs.srcs,
        strip_prefix = ctx.attrs.strip_prefix,
        haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo],
        registerer = ctx.attrs._ghc_pkg_registerer[RunInfo],
    )

    toolchain_libs = attr_deps_haskell_toolchain_libraries(ctx)
    toolchain_lib_dyn_infos = [dep.dynamic for dep in toolchain_libs]

    ctx.actions.dynamic_output_new(
        _write_package_conf(
            md_file = md_file,
            toolchain_lib_dyn_infos = toolchain_lib_dyn_infos,
            pkg_conf = pkg_conf.as_output(),
            db = db.as_output(),
            libname = libname,
            arg = arg,
        ),
    )

    return db

HaskellLibBuildOutput = record(
    hlib = HaskellLibraryInfo,
    solibs = dict[str, LinkedObject],
    link_infos = LinkInfos,
    compiled = CompileResultInfo,
    libs = list[Artifact],
    extra = list[Artifact],
)

def _get_haskell_shared_library_name_linker_flags(
        linker_type: LinkerType,
        soname: str) -> list[str]:
    if linker_type == LinkerType("gnu"):
        return ["-Wl,-soname,{}".format(soname)]
    elif linker_type == LinkerType("darwin"):
        # Passing `-install_name @rpath/...` or
        # `-Xlinker -install_name -Xlinker @rpath/...` instead causes
        # ghc-9.6.3: panic! (the 'impossible' happened)
        return ["-Wl,-install_name,@rpath/{}".format(soname)]
    else:
        fail("Unknown linker type '{}'.".format(linker_type))

_DynamicLinkSharedOptions = record(
    artifact_suffix = str,
    haskell_toolchain = Provider, #HaskellToolchainInfo,
    infos = LinkArgs,
    link_args = ArgLike,  # TODO: is this redundant with `infos`?
    haskell_direct_deps_lib_infos = list[HaskellLibraryInfo],
    direct_deps_info = list[HaskellLibraryInfoTSet],
    lib = Artifact,
    libfile = str,
    linker_flags = list[typing.Any],  # args
    linker_info = LinkerInfo,
    objects = list[Artifact],
    link_group_libs = list[HaskellLinkGroupInfo],
    toolchain_libs = list[str],
    project_libs = list[str],
    toolchain_libs_full = list[HaskellToolchainLibrary],
    project_libs_full = list[HaskellLibraryInfo],
    worker_target_id = str,
)

def _dynamic_link_shared_impl(
        actions: AnalysisActions,
        pkg_deps: ResolvedDynamicValue,
        lib: OutputArtifact,
        arg: _DynamicLinkSharedOptions) -> list[Provider]:
    # link group
    all_link_group_ids = [l.id for lg in arg.link_group_libs for l in lg.libraries]

    package_db = pkg_deps.providers[DynamicHaskellPackageDbInfo].packages

    libs = actions.tset(HaskellLibraryInfoTSet, children = arg.direct_deps_info)
    all_deps = libs.reduce("packages")
    package_db_tset = actions.tset(
        HaskellPackageDbTSet,
        children = [package_db[name] for name in (arg.toolchain_libs + all_deps) if name in package_db],
    )

    packagedb_args = cmd_args()
    for d in list(libs.traverse()):
        if d.name in all_link_group_ids:
            packagedb_args.add(cmd_args(d.empty_db))
        else:
            packagedb_args.add(cmd_args(d.db))
    for lg in arg.link_group_libs:
        packagedb_args.add(cmd_args(lg.db))

    packagedb_args.add(package_db_tset.project_as_args("package_db"))

    link_args = cmd_args()
    link_cmd_hidden = []

    link_args.add(arg.haskell_toolchain.linker_flags)
    link_args.add(arg.linker_flags)
    link_args.add("-hide-all-packages")
    link_args.add(cmd_args(packagedb_args, prepend = "-package-db"))

    link_args.add(cmd_args(arg.toolchain_libs, prepend = "-package"))

    for item in arg.haskell_direct_deps_lib_infos:
        if not item.id in all_link_group_ids:
            link_args.add(cmd_args(item.name, prepend = "-package"))

    link_args.add(cmd_args(package_db_tset.project_as_args("package_db"), prepend = "-package-db"))

    # link group
    for lg in arg.link_group_libs:
        link_args.add("-package", lg.pkgname)
        link_cmd_hidden.append(lg.lib)

    link_args.add(
        get_shared_library_flags(arg.linker_info.type),
        "-dynamic",
        cmd_args(
            _get_haskell_shared_library_name_linker_flags(arg.linker_info.type, arg.libfile),
            prepend = "-optl",
        ),
    )

    link_args.add(arg.objects)

    link_cmd_hidden.append(unpack_link_args(arg.infos))

    link_args.add(arg.link_args)

    link_cmd = cmd_args(
        arg.haskell_toolchain.linker,
        at_argfile(
            actions = actions,
            name = "haskell_link_" + arg.artifact_suffix.replace("-", "_") + ".argsfile",
            args = link_args,
            allow_args = True,
        ),
        "-o",
        lib,
        hidden = link_cmd_hidden,
    )

    actions.run(
        link_cmd,
        category = "haskell_link_" + arg.artifact_suffix.replace("-", "_"),
        # explicit turn this on for local_only actions to upload their results.
        allow_cache_upload = True,
    )

    return []

_dynamic_link_shared = dynamic_actions(
    impl = _dynamic_link_shared_impl,
    attrs = {
        "arg": dynattrs.value(typing.Any),
        "lib": dynattrs.output(),
        "pkg_deps": dynattrs.dynamic_value(),
    },
)

def _build_haskell_lib(
        ctx: AnalysisContext,
        worker: WorkerInfo | None,
        allow_worker: bool,
        libname: str,
        pkgname: str,
        hlis: list[HaskellLinkInfo],  # haskell link infos from all deps
        nlis: list[MergedLinkInfo],  # native link infos from all deps
        link_style: LinkStyle,
        enable_profiling: bool,
        enable_haddock: bool,
        md_file: Artifact,
        # The non-profiling artifacts are also needed to build the package for
        # profiling, so it should be passed when `enable_profiling` is True.
        non_profiling_hlib: [HaskellLibBuildOutput, None] = None) -> HaskellLibBuildOutput:
    linker_info = ctx.attrs._cxx_toolchain[CxxToolchainInfo].linker_info

    # Link the objects into a library
    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]

    # Compile the sources
    #
    # TODO: This computes `link_args` from `ctx.attrs.extra_libraries` like we
    # do below, I think it may put in duplicate `link_args` at some point.
    compiled = compile(
        ctx,
        link_style,
        enable_profiling = enable_profiling,
        enable_haddock = enable_haddock,
        md_file = md_file,
        pkgname = pkgname,
        worker = worker,
        incremental = ctx.attrs.incremental,
        is_haskell_binary = False,
    )
    solibs = {}
    artifact_suffix = get_artifact_suffix(link_style, enable_profiling)

    libstem = libname

    dynamic_lib_suffix = "." + LINKERS[linker_info.type].default_shared_library_extension
    static_lib_suffix = "_p.a" if enable_profiling else ".a"
    if link_style == LinkStyle("shared"):
        compiler_suffix = "-ghc{}".format(haskell_toolchain.compiler_major_version)
    else:
        compiler_suffix = ""
    libfile = "lib" + libstem + compiler_suffix + (dynamic_lib_suffix if link_style == LinkStyle("shared") else static_lib_suffix)

    lib_short_path = paths.join("lib-{}".format(artifact_suffix), libfile)

    linfos = [x.prof_info if enable_profiling else x.info for x in hlis]

    # only gather direct dependencies
    uniq_infos = [x[link_style].value for x in linfos]

    toolchain_libs = [dep.name for dep in attr_deps_haskell_toolchain_libraries(ctx)]
    project_libs = [dep.name for dep in attr_deps_haskell_lib_infos(ctx, link_style, enable_profiling)]
    toolchain_libs_full = attr_deps_haskell_toolchain_libraries(ctx)
    project_libs_full = attr_deps_haskell_lib_infos(ctx, link_style, enable_profiling)

    # extra-libraries
    link_args = unpack_link_args(get_link_args_for_strategy(
        ctx,
        [
            lib[MergedLinkInfo]
            for lib in ctx.attrs.extra_libraries
            if MergedLinkInfo in lib
        ],
        to_link_strategy(link_style),
    ))

    if link_style == LinkStyle("shared"):
        lib = ctx.actions.declare_output(lib_short_path)
        objects = [
            object
            for object in compiled.objects
            if not object.extension.endswith("-boot")
        ]

        infos = get_link_args_for_strategy(
            ctx,
            nlis,
            to_link_strategy(link_style),
        )

        haskell_direct_deps_lib_infos = attr_deps_haskell_lib_infos(
            ctx,
            link_style,
            enable_profiling = enable_profiling,
        )
        direct_deps_info = [
            lib.prof_info[link_style] if enable_profiling else lib.info[link_style]
            for lib in attr_deps_haskell_link_infos(ctx)
        ]
        link_group_libs = attr_deps_haskell_link_group_infos(ctx)

        ctx.actions.dynamic_output_new(_dynamic_link_shared(
            pkg_deps = haskell_toolchain.packages.dynamic,
            lib = lib.as_output(),
            arg = _DynamicLinkSharedOptions(
                artifact_suffix = artifact_suffix,
                haskell_toolchain = haskell_toolchain,
                infos = infos,
                haskell_direct_deps_lib_infos = haskell_direct_deps_lib_infos,
                direct_deps_info = direct_deps_info,
                lib = lib,
                libfile = libfile,
                linker_flags = ctx.attrs.linker_flags,
                linker_info = linker_info,
                objects = objects,
                link_group_libs = link_group_libs,
                toolchain_libs = toolchain_libs,
                project_libs = project_libs,
                toolchain_libs_full = toolchain_libs_full,
                project_libs_full = project_libs_full,
                worker_target_id = pkgname,
                link_args = link_args,
            ),
        ))

        if worker != None and allow_worker and haskell_toolchain.use_worker and not haskell_toolchain.worker_make:
            dummy = ctx.actions.declare_output("{}.metadata".format(lib_short_path))

            worker_close_cmd = cmd_args(ctx.attrs._ghc_wrapper[RunInfo])
            worker_close_cmd.add("--worker-close", "True")
            worker_close_cmd.add("--worker-target-id", to_hash(pkgname))
            worker_close_cmd.add("--close-input", lib)
            for hli in hlis:
                for e in hli.extra[link_style]:
                    worker_close_cmd.add("--close-input", e)

            worker_close_cmd.add("--close-output", dummy.as_output())
            worker_close_cmd.add("--buck2-dep", "dummy")
            worker_close_cmd.add("--buck2-packagedb-dep", "dummy")
            worker_close_cmd.add("--abi-out", "dummy")
            worker_close_cmd.add("--ghc", haskell_toolchain.compiler)

            worker_args = dict(exe = WorkerRunInfo(worker = worker))
            ctx.actions.run(worker_close_cmd, category = "worker_close", **worker_args)
            extra = [dummy]
        else:
            extra = []

        solibs[libfile] = LinkedObject(output = lib, unstripped_output = lib)
        libs = [lib]
        link_infos = LinkInfos(
            default = LinkInfo(linkables = [SharedLibLinkable(lib = lib)]),
        )

    else:  # static flavours
        # TODO: avoid making an archive for a single object, like cxx does
        # (but would that work with Template Haskell?)
        objs = [o for o in compiled.objects if o.extension != ".dyn_o"]
        archive = make_archive(ctx, lib_short_path, objs)
        lib = archive.artifact
        libs = [lib] + archive.external_objects
        link_infos = LinkInfos(
            default = LinkInfo(
                linkables = [
                    ArchiveLinkable(
                        archive = archive,
                        linker_type = linker_info.type,
                        link_whole = ctx.attrs.link_whole,
                    ),
                ],
            ),
        )
        extra = []

    if enable_profiling and link_style != LinkStyle("shared"):
        if not non_profiling_hlib:
            fail("Non-profiling HaskellLibBuildOutput wasn't provided when building profiling lib")

        dynamic = {
            True: compiled.module_tsets,
            False: non_profiling_hlib.compiled.module_tsets,
        }
        import_artifacts = {
            True: compiled.hi,
            False: non_profiling_hlib.compiled.hi,
        }
        object_artifacts = {
            True: compiled.objects,
            False: non_profiling_hlib.compiled.objects,
        }
        hie_artifacts = {
            True: compiled.hie,
            False: non_profiling_hlib.compiled.hie,
        }
        all_libs = libs + non_profiling_hlib.libs
        stub_dirs = [compiled.stubs] + [non_profiling_hlib.compiled.stubs]
    else:
        dynamic = {
            False: compiled.module_tsets,
        }
        import_artifacts = {
            False: compiled.hi,
        }
        object_artifacts = {
            False: compiled.objects,
        }
        hie_artifacts = {
            False: compiled.hie,
        }
        all_libs = libs
        stub_dirs = [compiled.stubs]

    db = _make_package(
        ctx,
        link_style,
        pkgname,
        libstem,
        uniq_infos,
        import_artifacts.keys(),
        enable_profiling = enable_profiling,
        use_empty_lib = False,
        md_file = md_file,
    )
    empty_db = _make_package(
        ctx,
        link_style,
        pkgname,
        None,
        uniq_infos,
        import_artifacts.keys(),
        enable_profiling = enable_profiling,
        use_empty_lib = True,
        md_file = md_file,
    )
    deps_db = _make_package(
        ctx,
        link_style,
        pkgname,
        None,
        uniq_infos,
        import_artifacts.keys(),
        enable_profiling = enable_profiling,
        use_empty_lib = True,
        md_file = md_file,
        for_deps = True,
    )

    hlib = HaskellLibraryInfo(
        name = pkgname,
        db = db,
        empty_db = empty_db,
        deps_db = deps_db,
        id = pkgname,
        dynamic = dynamic,  # TODO(ah) refine with dynamic projections
        import_dirs = import_artifacts,
        objects = object_artifacts,
        hie_files = hie_artifacts,
        stub_dirs = stub_dirs,
        extra_libraries = ctx.attrs.extra_libraries,
        libs = all_libs,
        version = "1.0.0",
        is_prebuilt = False,
        profiling_enabled = enable_profiling,
        dependencies = toolchain_libs + project_libs,
        toolchain_dependencies = toolchain_libs_full,
        md_file = md_file,
    )

    return HaskellLibBuildOutput(
        hlib = hlib,
        solibs = solibs,
        link_infos = link_infos,
        compiled = compiled,
        libs = libs,
        extra = extra,
    )

def haskell_library_impl(ctx: AnalysisContext) -> list[Provider]:
    sources = ctx.attrs.srcs
    error_on_non_haskell_srcs(sources, ctx.label)

    preferred_linkage = _attr_preferred_linkage(ctx)
    if ctx.attrs.enable_profiling and preferred_linkage == Linkage("any"):
        preferred_linkage = Linkage("static")

    # Get haskell and native link infos from all deps
    hlis = attr_deps_haskell_link_infos_sans_template_deps(ctx)
    nlis = attr_deps_merged_link_infos(ctx)
    prof_nlis = attr_deps_profiling_link_infos(ctx)
    shared_library_infos = attr_deps_shared_library_infos(ctx)

    solibs = {}
    link_infos = {}
    prof_link_infos = {}
    hlib_infos = {}
    hlink_infos = {}
    prof_hlib_infos = {}
    prof_hlink_infos = {}
    indexing_tsets = {}
    sub_targets = {}
    extra = {}

    if ctx.attrs.use_same_package_name:
        libname = ctx.label.name
        pkgname = libname
    else:
        libprefix = repr(ctx.label.path).replace("//", "_").replace("/", "_")

        # avoid consecutive "--" in package name, which is not allowed by ghc-pkg.
        if libprefix[-1] == "_":
            libname0 = libprefix + ctx.label.name
        else:
            libname0 = libprefix + "_" + ctx.label.name
        pkgname = libname0.replace("_", "-")
        libname = "HS" + pkgname

    worker = ctx.attrs._worker[WorkerInfo] if ctx.attrs._worker else None

    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]

    # The non-profiling library is also needed to build the package with
    # profiling enabled, so we need to keep track of it for each link style.
    non_profiling_hlib = {}
    def_md_file = None
    for enable_profiling in [False, True]:
        for output_style in get_output_styles_for_linkage(preferred_linkage):
            link_style = legacy_output_style_to_link_style(output_style)
            if link_style == LinkStyle("shared") and enable_profiling:
                # Profiling isn't support with dynamic linking
                continue

            # Request the build plan from GHC in order to resolve dependencies between modules.
            # This is executed for each output style even though the dependency graph is independent of it.
            # The reason for that is that the persistent worker initializes the module graph fully during this request,
            # requiring the linking and profiling settings to be applied.
            md_file = target_metadata(
                ctx,
                link_style = link_style,
                enable_profiling = enable_profiling,
                enable_haddock = not enable_profiling and not non_profiling_hlib,
                main = None,
                sources = sources,
                worker = worker,
            )
            if link_style == LinkStyle("shared") and not enable_profiling:
                def_md_file = md_file

            hlib_build_out = _build_haskell_lib(
                ctx,
                worker,
                ctx.attrs.allow_worker,
                libname,
                pkgname,
                hlis = hlis,
                nlis = nlis,
                link_style = link_style,
                enable_profiling = enable_profiling,
                # enable haddock only for the first non-profiling hlib
                enable_haddock = not enable_profiling and not non_profiling_hlib,
                md_file = md_file,
                non_profiling_hlib = non_profiling_hlib.get(link_style),
            )
            if not enable_profiling:
                non_profiling_hlib[link_style] = hlib_build_out

            hlib = hlib_build_out.hlib
            solibs.update(hlib_build_out.solibs)
            compiled = hlib_build_out.compiled
            libs = hlib_build_out.libs
            extra[link_style] = hlib_build_out.extra

            if enable_profiling:
                prof_hlib_infos[link_style] = hlib
                prof_hlink_infos[link_style] = ctx.actions.tset(HaskellLibraryInfoTSet, value = hlib, children = [li.prof_info[link_style] for li in hlis])
                prof_link_infos[link_style] = hlib_build_out.link_infos
            else:
                hlib_infos[link_style] = hlib
                hlink_infos[link_style] = ctx.actions.tset(HaskellLibraryInfoTSet, value = hlib, children = [li.info[link_style] for li in hlis])
                link_infos[link_style] = hlib_build_out.link_infos

            # Build the indices and create subtargets only once, with profiling
            # enabled or disabled based on what was set in the library's
            # target.
            if ctx.attrs.enable_profiling == enable_profiling:
                if compiled.producing_indices:
                    tset = derive_indexing_tset(
                        ctx.actions,
                        link_style,
                        compiled.hi,
                        attr_deps(ctx),
                    )
                    indexing_tsets[link_style] = tset

                sub_targets[link_style.value.replace("_", "-")] = [DefaultInfo(
                    default_outputs = libs,
                    sub_targets = _haskell_module_sub_targets(
                        compiled = compiled,
                        link_style = link_style,
                        enable_profiling = enable_profiling,
                    ) | dict(metadata = [DefaultInfo(default_output = md_file)]),
                )]

    # By default, [metadata] = [shared][metadata].
    if def_md_file:
        sub_targets["metadata"] = [DefaultInfo(default_output = def_md_file)]

    pic_behavior = ctx.attrs._cxx_toolchain[CxxToolchainInfo].pic_behavior
    link_style = cxx_toolchain_link_style(ctx)
    output_style = get_lib_output_style(
        to_link_strategy(link_style),
        preferred_linkage,
        pic_behavior,
    )
    shared_libs = create_shared_libraries(ctx, solibs)

    # TODO(cjhopman): this haskell implementation does not consistently handle LibOutputStyle
    # and LinkStrategy as expected and it's hard to tell what the intent of the existing code is
    # and so we currently just preserve its existing use of the legacy LinkStyle type and just
    # naively convert it at the boundaries of other code. This needs to be cleaned up by someone
    # who understands the intent of the code here.
    actual_link_style = legacy_output_style_to_link_style(output_style)

    if preferred_linkage != Linkage("static"):
        # Profiling isn't support with dynamic linking, but `prof_link_infos`
        # needs entries for all link styles.
        # We only need to set the shared link_style in both `prof_link_infos`
        # and `link_infos` if the target doesn't force static linking.
        prof_link_infos[LinkStyle("shared")] = link_infos[LinkStyle("shared")]

    default_link_infos = prof_link_infos if ctx.attrs.enable_profiling else link_infos
    default_native_infos = prof_nlis if ctx.attrs.enable_profiling else nlis
    merged_link_info = create_merged_link_info(
        ctx,
        pic_behavior = pic_behavior,
        link_infos = {_to_lib_output_style(s): v for s, v in default_link_infos.items()},
        preferred_linkage = preferred_linkage,
        exported_deps = default_native_infos,
    )

    prof_merged_link_info = create_merged_link_info(
        ctx,
        pic_behavior = pic_behavior,
        link_infos = {_to_lib_output_style(s): v for s, v in prof_link_infos.items()},
        preferred_linkage = preferred_linkage,
        exported_deps = prof_nlis,
    )

    linkable_graph = create_linkable_graph(
        ctx,
        node = create_linkable_graph_node(
            ctx,
            linkable_node = create_linkable_node(
                ctx = ctx,
                preferred_linkage = preferred_linkage,
                exported_deps = ctx.attrs.deps,
                link_infos = {_to_lib_output_style(s): v for s, v in link_infos.items()},
                shared_libs = shared_libs,
                # TODO(cjhopman): this should be set to non-None
                default_soname = None,
            ),
        ),
        deps = ctx.attrs.deps,
    )

    default_output = hlib_infos[actual_link_style].libs + extra[actual_link_style]

    inherited_pp_info = cxx_inherited_preprocessor_infos(attr_deps(ctx))

    # We would like to expose the generated _stub.h headers to C++
    # compilations, but it's hard to do that without overbuilding. Which
    # link_style should we pick below? If we pick a different link_style from
    # the one being used by the root rule, we'll end up building all the
    # Haskell libraries multiple times.
    #
    #    pp = [CPreprocessor(
    #        args =
    #            flatten([["-isystem", dir] for dir in hlib_infos[actual_link_style].stub_dirs]),
    #    )]
    pp = []

    haddock = haskell_haddock_lib(
        ctx,
        pkgname,
        non_profiling_hlib[LinkStyle("shared")].compiled,
        md_file,
    )

    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]

    styles = [
        ctx.actions.declare_output("haddock-html", file)
        for file in "synopsis.png linuwial.css quick-jump.css haddock-bundle.min.js".split()
    ]
    ctx.actions.run(
        cmd_args(
            haskell_toolchain.haddock,
            "--gen-index",
            "--optghc=-package-env=-",
            "-o",
            cmd_args(styles[0].as_output(), parent = 1),
            hidden = [file.as_output() for file in styles],
        ),
        category = "haddock_styles",
    )
    sub_targets.update({
        "haddock": [DefaultInfo(
            default_outputs = haddock.html.values(),
            sub_targets = {
                module: [DefaultInfo(default_output = html, other_outputs = styles)]
                for module, html in haddock.html.items()
            },
        )],
    })

    providers = [
        DefaultInfo(
            default_outputs = default_output,
            sub_targets = sub_targets,
        ),
        HaskellLibraryProvider(
            lib = hlib_infos,
            prof_lib = prof_hlib_infos,
        ),
        HaskellLinkInfo(
            info = hlink_infos,
            prof_info = prof_hlink_infos,
            extra = extra,
        ),
        merged_link_info,
        HaskellProfLinkInfo(
            prof_infos = prof_merged_link_info,
        ),
        linkable_graph,
        cxx_merge_cpreprocessors(ctx, pp, inherited_pp_info),
        merge_shared_libraries(
            ctx.actions,
            shared_libs,
            shared_library_infos,
        ),
        haddock,
    ]

    if indexing_tsets:
        providers.append(HaskellIndexInfo(info = indexing_tsets))

    # TODO(cjhopman): This code is for templ_vars is duplicated from cxx_library
    templ_vars = {}

    # Add in ldflag macros.
    for link_style in (LinkStyle("static"), LinkStyle("static_pic")):
        name = "ldflags-" + link_style.value.replace("_", "-")
        args = cmd_args()
        linker_info = ctx.attrs._cxx_toolchain[CxxToolchainInfo].linker_info
        args.add(linker_info.linker_flags)
        args.add(unpack_link_args(
            get_link_args_for_strategy(
                ctx,
                [merged_link_info],
                to_link_strategy(link_style),
            ),
        ))
        templ_vars[name] = args

    # TODO(T110378127): To implement `$(ldflags-shared ...)` properly, we'd need
    # to setup a symink tree rule for all transitive shared libs.  Since this
    # currently would be pretty costly (O(N^2)?), and since it's not that
    # commonly used anyway, just use `static-pic` instead.  Longer-term, once
    # v1 is gone, macros that use `$(ldflags-shared ...)` (e.g. Haskell's
    # hsc2hs) can move to a v2 rules-based API to avoid needing this macro.
    templ_vars["ldflags-shared"] = templ_vars["ldflags-static-pic"]

    providers.append(TemplatePlaceholderInfo(keyed_variables = templ_vars))

    providers.append(merge_link_group_lib_info(deps = attr_deps(ctx)))

    return providers

# TODO(cjhopman): should this be LibOutputType or LinkStrategy?
def derive_indexing_tset(
        actions: AnalysisActions,
        link_style: LinkStyle,
        value: list[Artifact] | None,
        children: list[Dependency]) -> HaskellIndexingTSet:
    index_children = []
    for dep in children:
        li = dep.get(HaskellIndexInfo)
        if li:
            if (link_style in li.info):
                index_children.append(li.info[link_style])

    return actions.tset(
        HaskellIndexingTSet,
        value = value,
        children = index_children,
    )

def _make_link_package(
        ctx: AnalysisContext,
        link_style: LinkStyle,
        pkgname: str,
        hlis: list[HaskellLibraryInfo],
        static_libs: ArgLike) -> Artifact:
    artifact_suffix = get_artifact_suffix(link_style, False)

    conf = cmd_args(
        "name: " + pkgname,
        "version: 1.0.0",
        "id: " + pkgname,
        "key: " + pkgname,
        "exposed: False",
        cmd_args(cmd_args(static_libs, delimiter = ", "), format = "ld-options: {}"),
        "depends: " + ", ".join([lib.id for lib in hlis]),
    )

    pkg_conf = ctx.actions.write("pkg-" + artifact_suffix + "_link.conf", conf)
    db = ctx.actions.declare_output("db-" + artifact_suffix + "_link", dir = True)

    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]
    registerer = ctx.attrs._ghc_pkg_registerer[RunInfo]
    category_prefix = "haskell_package_link_"

    _register_package_conf(
        ctx.actions,
        pkg_conf,
        db.as_output(),
        registerer,
        haskell_toolchain.packager,
        category_prefix,
        artifact_suffix,
        False,
    )

    return db

_DynamicLinkBinaryOptions = record(
    deps = list[Dependency],
    direct_deps_link_info = list[HaskellLinkInfo],
    enable_profiling = bool,
    haskell_direct_deps_lib_infos = list[HaskellLibraryInfo],
    haskell_toolchain = HaskellToolchainInfo,
    link_args = cmd_args,
    link_style = LinkStyle,
    linker_flags = list[typing.Any],  # Arguments.
    direct_deps_info = list[HaskellLibraryInfoTSet],
    link_group_libs = list[HaskellLinkGroupInfo],
    toolchain_libs = list[str],
)

def _dynamic_link_binary_impl(
        actions: AnalysisActions,
        pkg_deps: ResolvedDynamicValue,
        output: OutputArtifact,
        arg: _DynamicLinkBinaryOptions) -> list[Provider]:
    link_args = arg.link_args.copy()  # link is already frozen, make a copy
    link_cmd_hidden = []

    package_db = pkg_deps.providers[DynamicHaskellPackageDbInfo].packages

    link_args.add("-hide-all-packages")

    all_link_group_ids = [l.id for lg in arg.link_group_libs for l in lg.libraries]

    libs = actions.tset(HaskellLibraryInfoTSet, children = arg.direct_deps_info)

    all_toolchain_libs = arg.toolchain_libs + libs.reduce("packages")

    toolchain_package_db_tset = actions.tset(
        HaskellPackageDbTSet,
        children = [package_db[name] for name in all_toolchain_libs if name in package_db],
    )

    packagedb_args = cmd_args()

    for d in list(libs.traverse()):
        if d.name in all_link_group_ids:
            packagedb_args.add(cmd_args(d.empty_db))
        else:
            packagedb_args.add(cmd_args(d.db))
    for lg in arg.link_group_libs:
        packagedb_args.add(cmd_args(lg.db))
    packagedb_args.add(toolchain_package_db_tset.project_as_args("package_db"))

    link_args.add(cmd_args(packagedb_args, prepend = "-package-db"))

    link_args.add(cmd_args(arg.toolchain_libs, prepend = "-package"))
    for item in arg.haskell_direct_deps_lib_infos:
        if not item.id in all_link_group_ids:
            link_args.add(cmd_args(item.name, prepend = "-package"))
            link_cmd_hidden.append(item.libs)

    # link group
    for lg in arg.link_group_libs:
        link_args.add("-package", lg.pkgname)
        link_cmd_hidden.append(lg.lib)

    link_args.add(arg.haskell_toolchain.linker_flags)
    link_args.add(arg.linker_flags)

    link_args.add("-o", output)

    artifact_suffix = get_artifact_suffix(arg.link_style, arg.enable_profiling)
    link_cmd = cmd_args(
        arg.haskell_toolchain.compiler,
        at_argfile(
            actions = actions,
            name = "haskell_link_" + artifact_suffix.replace("-", "_") + ".argsfile",
            args = link_args,
            allow_args = True,
        ),
        hidden = link_cmd_hidden,
    )

    actions.run(
        link_cmd,
        category = "haskell_link",
        # explicit turn this on for local_only actions to upload their results.
        allow_cache_upload = True,
    )

    return []

_dynamic_link_binary = dynamic_actions(
    impl = _dynamic_link_binary_impl,
    attrs = {
        "arg": dynattrs.value(typing.Any),
        "pkg_deps": dynattrs.option(dynattrs.dynamic_value()),
        "output": dynattrs.output(),
    },
)

def haskell_binary_impl(ctx: AnalysisContext) -> list[Provider]:
    sources = ctx.attrs.srcs
    error_on_non_haskell_srcs(sources, ctx.label)

    enable_profiling = ctx.attrs.enable_profiling

    # Decide what kind of linking we're doing

    link_style = attr_link_style(ctx)

    # Link Groups
    link_group_info = get_link_group_info(ctx, filter_and_map_idx(LinkableGraph, attr_deps(ctx)))

    # Profiling doesn't support shared libraries
    if enable_profiling and link_style == LinkStyle("shared"):
        link_style = LinkStyle("static")

    worker = ctx.attrs._worker[WorkerInfo] if ctx.attrs._worker else None

    md_file = target_metadata(
        ctx,
        link_style = link_style,
        enable_profiling = enable_profiling,
        enable_haddock = False,
        main = getattr(ctx.attrs, "main", None),
        sources = sources,
        worker = worker,
    )

    # Provisional hack to have a worker ID
    libname = repr(ctx.label.path).replace("//", "_").replace("/", "_") + "_" + ctx.label.name
    pkgname = libname.replace("_", "-")

    compiled = compile(
        ctx,
        link_style,
        incremental = ctx.attrs.incremental,
        enable_profiling = enable_profiling,
        enable_haddock = False,
        md_file = md_file,
        worker = worker,
        pkgname = pkgname,
        is_haskell_binary = True,
    )

    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]

    toolchain_libs = [dep[HaskellToolchainLibrary].name for dep in ctx.attrs.deps if HaskellToolchainLibrary in dep]

    output = ctx.actions.declare_output(ctx.label.name)
    link_args = cmd_args()

    objects = {}

    # extra-libraries
    link_args.add(unpack_link_args(get_link_args_for_strategy(
        ctx,
        [
            lib[MergedLinkInfo]
            for lib in ctx.attrs.extra_libraries
            if MergedLinkInfo in lib
        ],
        to_link_strategy(link_style),
    )))

    # only add the first object per module
    # TODO[CB] restructure this to use a record / dict for compiled.objects
    for obj in compiled.objects:
        key = paths.replace_extension(obj.short_path, "")
        if not key in objects:
            objects[key] = obj

    link_args.add(objects.values())

    indexing_tsets = {}
    if compiled.producing_indices:
        tset = derive_indexing_tset(ctx.actions, link_style, compiled.hi, attr_deps(ctx))
        indexing_tsets[link_style] = tset

    slis = []
    for lib in attr_deps(ctx):
        li = lib.get(SharedLibraryInfo)
        if li != None:
            slis.append(li)
    shlib_info = merge_shared_libraries(
        ctx.actions,
        deps = slis,
    )

    sos = []

    link_strategy = to_link_strategy(link_style)
    if link_group_info != None:
        own_binary_link_flags = []
        auto_link_groups = {}
        link_group_libs = {}
        link_deps = linkables(attr_deps(ctx))
        linkable_graph_node_map = get_linkable_graph_node_map_func(link_group_info.graph)()
        link_group_preferred_linkage = get_link_group_preferred_linkage(link_group_info.groups.values())

        # If we're using auto-link-groups, where we generate the link group links
        # in the prelude, the link group map will give us the link group libs.
        # Otherwise, pull them from the `LinkGroupLibInfo` provider from out deps.
        auto_link_group_specs = get_auto_link_group_specs(ctx, link_group_info)
        executable_deps = [d.linkable_graph.nodes.value.label for d in link_deps if d.linkable_graph != None]
        public_nodes = get_public_link_group_nodes(
            linkable_graph_node_map,
            link_group_info.mappings,
            executable_deps,
            None,
        )
        if auto_link_group_specs != None:
            linked_link_groups = create_link_groups(
                ctx = ctx,
                link_strategy = link_strategy,
                link_group_mappings = link_group_info.mappings,
                link_group_preferred_linkage = link_group_preferred_linkage,
                executable_deps = executable_deps,
                link_group_specs = auto_link_group_specs,
                linkable_graph_node_map = linkable_graph_node_map,
                public_nodes = public_nodes,
            )
            for name, linked_link_group in linked_link_groups.libs.items():
                auto_link_groups[name] = linked_link_group.artifact
                if linked_link_group.library != None:
                    link_group_libs[name] = linked_link_group.library
            own_binary_link_flags += linked_link_groups.symbol_ldflags

        else:
            # NOTE(agallagher): We don't use version scripts and linker scripts
            # for non-auto-link-group flow, as it's note clear it's useful (e.g.
            # it's mainly for supporting dlopen-enabled libs and extensions).
            link_group_libs = gather_link_group_libs(
                children = [d.link_group_lib_info for d in link_deps],
            )

        link_group_relevant_roots = find_relevant_roots(
            linkable_graph_node_map = linkable_graph_node_map,
            link_group_mappings = link_group_info.mappings,
            roots = get_dedupped_roots_from_groups(link_group_info.groups.values()),
        )

        labels_to_links = get_filtered_labels_to_links_map(
            public_nodes = public_nodes,
            linkable_graph_node_map = linkable_graph_node_map,
            link_group = None,
            link_groups = link_group_info.groups,
            link_group_mappings = link_group_info.mappings,
            link_group_preferred_linkage = link_group_preferred_linkage,
            link_group_libs = {
                name: (lib.label, lib.shared_link_infos)
                for name, lib in link_group_libs.items()
            },
            link_strategy = link_strategy,
            roots = (
                [
                    d.linkable_graph.nodes.value.label
                    for d in link_deps
                    if d.linkable_graph != None
                ] +
                link_group_relevant_roots
            ),
            is_executable_link = True,
            force_static_follows_dependents = True,
            pic_behavior = PicBehavior("supported"),
        )

        # NOTE: Our Haskell DLL support impl currently links transitive haskell
        # deps needed by DLLs which get linked into the main executable as link-
        # whole.  To emulate this, we mark Haskell rules with a special label
        # and traverse this to find all the nodes we need to link whole.
        public_nodes = []
        if ctx.attrs.link_group_public_deps_label != None:
            public_nodes = get_transitive_deps_matching_labels(
                linkable_graph_node_map = linkable_graph_node_map,
                label = ctx.attrs.link_group_public_deps_label,
                roots = link_group_relevant_roots,
            )

        link_infos = []
        link_infos.append(
            LinkInfo(
                pre_flags = own_binary_link_flags,
            ),
        )
        link_infos.extend(get_filtered_links(labels_to_links.map, set(public_nodes)))
        infos = LinkArgs(infos = link_infos)

        link_group_ctx = LinkGroupContext(
            link_group_mappings = link_group_info.mappings,
            link_group_libs = link_group_libs,
            link_group_preferred_linkage = link_group_preferred_linkage,
            labels_to_links_map = labels_to_links.map,
            targets_consumed_by_link_groups = {},
        )

        for shared_lib in traverse_shared_library_info(shlib_info):
            label = shared_lib.label
            if is_link_group_shlib(label, link_group_ctx):
                sos.append(shared_lib)

        # When there are no matches for a pattern based link group,
        # `link_group_mappings` will not have an entry associated with the lib.
        for _name, link_group_lib in link_group_libs.items():
            sos.extend(link_group_lib.shared_libs.libraries)

    else:
        nlis = []
        for lib in attr_deps(ctx):
            if enable_profiling:
                hli = lib.get(HaskellProfLinkInfo)
                if hli != None:
                    nlis.append(hli.prof_infos)
                    continue
            li = lib.get(MergedLinkInfo)
            if li != None:
                nlis.append(li)
        sos.extend(traverse_shared_library_info(shlib_info))
        infos = get_link_args_for_strategy(ctx, nlis, to_link_strategy(link_style))

    if link_style in [LinkStyle("static"), LinkStyle("static_pic")]:
        hlis = attr_deps_haskell_link_infos_sans_template_deps(ctx)
        linfos = [x.prof_info if enable_profiling else x.info for x in hlis]
        uniq_infos = [x[link_style].value for x in linfos]

        pkgname = ctx.label.name.replace("_", "-") + "-link"
        linkable_artifacts = [
            f.archive.artifact
            for link in infos.tset.infos.traverse(ordering = "topological")
            for f in link.default.linkables
        ]
        db = _make_link_package(
            ctx,
            link_style,
            pkgname,
            uniq_infos,
            linkable_artifacts,
        )

        link_args.add(cmd_args(db, prepend = "-package-db"))
        link_args.add("-package", pkgname)
        link_args.add(cmd_args(hidden = linkable_artifacts))
    else:
        link_args.add("-dynamic")

    haskell_direct_deps_lib_infos = attr_deps_haskell_lib_infos(
        ctx,
        link_style,
        enable_profiling = enable_profiling,
    )

    direct_deps_info = [
        lib.prof_info[link_style] if enable_profiling else lib.info[link_style]
        for lib in attr_deps_haskell_link_infos(ctx)
    ]
    link_group_libs = attr_deps_haskell_link_group_infos(ctx)

    ctx.actions.dynamic_output_new(_dynamic_link_binary(
        pkg_deps = haskell_toolchain.packages.dynamic if haskell_toolchain.packages else None,
        output = output.as_output(),
        arg = _DynamicLinkBinaryOptions(
            deps = ctx.attrs.deps,
            direct_deps_link_info = attr_deps_haskell_link_infos(ctx),
            enable_profiling = enable_profiling,
            haskell_direct_deps_lib_infos = haskell_direct_deps_lib_infos,
            haskell_toolchain = haskell_toolchain,
            link_args = link_args,
            link_style = link_style,
            linker_flags = ctx.attrs.linker_flags,
            direct_deps_info = direct_deps_info,
            link_group_libs = link_group_libs,
            toolchain_libs = toolchain_libs,
        ),
    ))

    if link_style == LinkStyle("shared") or link_group_info != None:
        sos_dir = "__{}__shared_libs_symlink_tree".format(ctx.label.name)
        symlink_dir = create_shlib_symlink_tree(
            actions = ctx.actions,
            out = sos_dir,
            shared_libs = sos,
        )
        run = cmd_args(output, hidden = symlink_dir)
    else:
        run = cmd_args(output)

    sub_targets = {
        "metadata": [DefaultInfo(default_output = md_file)],
    }
    sub_targets.update(_haskell_module_sub_targets(
        compiled = compiled,
        link_style = link_style,
        enable_profiling = enable_profiling,
    ))

    providers = [
        DefaultInfo(
            default_output = output,
            sub_targets = sub_targets,
        ),
        RunInfo(args = run),
    ]

    if indexing_tsets:
        providers.append(HaskellIndexInfo(info = indexing_tsets))

    return providers

def _haskell_module_sub_targets(
        *,
        compiled: CompileResultInfo,
        link_style: LinkStyle,
        enable_profiling: bool) -> dict[str, list[Provider]]:
    (osuf, hisuf) = output_extensions(link_style, enable_profiling)
    return {
        "interfaces": [DefaultInfo(sub_targets = {
            src_to_module_name(hi.short_path): [DefaultInfo(default_output = hi)]
            for hi in compiled.hi
            if hi.extension[1:] == hisuf
        })],
        "objects": [DefaultInfo(sub_targets = {
            src_to_module_name(o.short_path): [DefaultInfo(default_output = o)]
            for o in compiled.objects
            if o.extension[1:] == osuf
        })],
        "hie": [DefaultInfo(sub_targets = {
            src_to_module_name(hie.short_path): [DefaultInfo(default_output = hie)]
            for hie in compiled.hie
            if hie.extension == ".hie"
        })],
    }

#
def _make_link_group_package(
        actions: AnalysisActions,
        *,
        link_style: LinkStyle,
        link_infos: list[LinkInfo],
        pkgname: str,
        libname: str,
        registerer: RunInfo,
        haskell_toolchain: HaskellToolchainInfo,
        db: OutputArtifact,
        hlibs: list[HaskellLibraryInfo],
        project_deps: list[str],
        toolchain_lib_dyn_infos: list[ResolvedDynamicValue]) -> None:
    artifact_suffix = get_artifact_suffix(link_style, False)

    toolchain_deps = [info.providers[DynamicHaskellToolchainLibraryInfo].id for info in toolchain_lib_dyn_infos]
    direct_deps = [lib.name for lib in hlibs]
    indirect_deps = [n for n in project_deps if n not in direct_deps]
    all_deps = indirect_deps + toolchain_deps

    conf = cmd_args(
        "name: " + pkgname,
        "version: 1.0.0",
        "id: " + pkgname,
        "key: " + pkgname,
        "exposed: False",
        "depends: " + ", ".join(all_deps),
    )

    profiled = False
    library_dirs = [_mk_artifact_dir("lib", profiled, link_style)]
    conf.add(cmd_args(cmd_args(library_dirs, delimiter = ","), format = "library-dirs: {}"))
    conf.add(cmd_args(libname, format = "hs-libraries: {}"))

    # collect all the extra library dependencies from component Haskell libraries
    append_pkg_conf_link_fields_for_link_infos(
        pkgname = pkgname,
        pkg_conf = conf,
        link_infos = link_infos,
    )

    pkg_conf = actions.write("pkg-" + artifact_suffix, conf)

    _register_package_conf(
        actions,
        pkg_conf,
        db,
        registerer,
        haskell_toolchain.packager,
        "haskell_package_linkgroup_",
        artifact_suffix,
        False,
    )

_DynamicLinkGroupSharedOptions = record(
    hlibs = list[HaskellLibraryInfo],
    pkgname = str,
    libname = str,
    libfile = str,
    linker_info = LinkerInfo,
    registerer = RunInfo,
    haskell_toolchain = HaskellToolchainInfo,
    toolchain_deps = list[HaskellToolchainLibrary],
    project_deps = list[str],
    libs_tset = HaskellLibraryInfoTSet,
    link_args = LinkArgs,
)

# Implement dynamic library linking for a link group
def _dynamic_link_group_shared_impl(
        actions: AnalysisActions,
        lib: OutputArtifact,
        db: OutputArtifact,
        arg: _DynamicLinkGroupSharedOptions,
        toolchain_lib_dyn_infos: list[ResolvedDynamicValue],
        pkg_deps: ResolvedDynamicValue | None):
    link_cmd_hidden = []
    link_args = cmd_args()
    link_args.add(arg.haskell_toolchain.linker_flags)

    toolchain_deps = [d.name for d in arg.toolchain_deps]
    package_db = pkg_deps.providers[DynamicHaskellPackageDbInfo].packages

    package_db_tset = actions.tset(
        HaskellPackageDbTSet,
        children = [package_db[name] for name in toolchain_deps if name in package_db],
    )
    packagedb_args = cmd_args()
    packagedb_args.add(package_db_tset.project_as_args("package_db"))

    # adding indirect project dep packages
    direct_deps = []
    indirect_deps = []
    direct_deps_name = [d.name for d in arg.hlibs]
    for d in list(arg.libs_tset.traverse()):
        if d.name in direct_deps_name:
            direct_deps.append(d)
            packagedb_args.add(cmd_args(d.empty_db))
        else:
            indirect_deps.append(d)
            packagedb_args.add(cmd_args(d.db))

    link_args.add(cmd_args(packagedb_args, prepend = "-package-db"))
    for d in indirect_deps:
        link_args.add(cmd_args(d.name, prepend = "-package"))
        link_cmd_hidden.append(d.libs)

    # adding toolchain dep packages
    link_args.add(cmd_args(toolchain_deps, prepend = "-package"))

    for hlib in arg.hlibs:
        is_profiled = False
        for o in hlib.objects[is_profiled]:
            link_args.add(o)

    link_args.add(unpack_link_args(arg.link_args))

    link_args.add(
        get_shared_library_flags(arg.linker_info.type),
        "-dynamic",
        cmd_args(
            _get_haskell_shared_library_name_linker_flags(arg.linker_info.type, arg.libfile),
            prepend = "-optl",
        ),
    )

    link_cmd = cmd_args(
        arg.haskell_toolchain.linker,
        at_argfile(
            actions = actions,
            name = "haskell_link_group_shared.argsfile",
            args = link_args,
            allow_args = True,
        ),
        "-o",
        lib,
        hidden = link_cmd_hidden,
    )

    actions.run(
        link_cmd,
        category = "haskell_link_group_shared",
        identifier = arg.libname,
        allow_cache_upload = True,
    )

    _make_link_group_package(
        actions,
        link_style = LinkStyle("shared"),
        link_infos = map_to_link_infos([arg.link_args]),
        pkgname = arg.pkgname,
        libname = arg.libname,
        registerer = arg.registerer,
        haskell_toolchain = arg.haskell_toolchain,
        db = db,
        hlibs = arg.hlibs,
        project_deps = arg.project_deps,
        toolchain_lib_dyn_infos = toolchain_lib_dyn_infos,
    )

    return []

_dynamic_link_group_shared = dynamic_actions(
    impl = _dynamic_link_group_shared_impl,
    attrs = {
        "lib": dynattrs.output(),
        "db": dynattrs.output(),
        "arg": dynattrs.value(typing.Any),
        "toolchain_lib_dyn_infos": dynattrs.list(dynattrs.dynamic_value()),
        "pkg_deps": dynattrs.option(dynattrs.dynamic_value()),
    },
)

# Haskell link group implementation
# Link group creates a virtual package with only shared and static library artifacts
# This saves linking time.
def make_haskell_link_group(
        actions: AnalysisActions,
        *,
        label: Label,
        hlibs: list[HaskellLibraryInfo],
        direct_deps_info: list[HaskellLibraryInfoTSet],
        link_style: LinkStyle,
        enable_profiling: bool,
        registerer: RunInfo,
        haskell_toolchain: HaskellToolchainInfo,
        linker_info: LinkerInfo,
        link_args: LinkArgs) -> list[Provider]:
    artifact_suffix = get_artifact_suffix(link_style, enable_profiling)
    dynamic_lib_suffix = "." + LINKERS[linker_info.type].default_shared_library_extension
    static_lib_suffix = "_p.a" if enable_profiling else ".a"

    libprefix = repr(label.path).replace("//", "_").replace("/", "_")

    # avoid consecutive "--" in package name, which is not allowed by ghc-pkg.
    if libprefix[-1] == "_":
        libname0 = libprefix + label.name
    else:
        libname0 = libprefix + "_" + label.name
    pkgname = libname0.replace("_", "-")
    libname = "HS" + pkgname

    libstem = libname
    if link_style == LinkStyle("shared"):
        compiler_suffix = "-ghc{}".format(haskell_toolchain.compiler_major_version)
    else:
        compiler_suffix = ""
    libfile = "lib" + libstem + compiler_suffix + (dynamic_lib_suffix if link_style == LinkStyle("shared") else static_lib_suffix)

    lib_short_path = paths.join("lib-{}".format(artifact_suffix), libfile)
    lib = actions.declare_output(lib_short_path)
    db = actions.declare_output("db-" + artifact_suffix, dir = True)

    libs_tset = actions.tset(
        HaskellLibraryInfoTSet,
        children = direct_deps_info,
    )

    toolchain_deps = libs_tset.reduce("toolchain_packages")
    toolchain_deps_name = [d.name for d in toolchain_deps]
    toolchain_lib_dyn_infos = [dep.dynamic for dep in toolchain_deps]

    all_deps = libs_tset.reduce("packages")
    project_deps = [d for d in all_deps if d not in toolchain_deps_name]

    pkg_deps = haskell_toolchain.packages.dynamic if haskell_toolchain.packages else None

    actions.dynamic_output_new(_dynamic_link_group_shared(
        lib = lib.as_output(),
        db = db.as_output(),
        arg = _DynamicLinkGroupSharedOptions(
            hlibs = hlibs,
            pkgname = pkgname,
            libname = libname,
            libfile = libfile,
            linker_info = linker_info,
            registerer = registerer,
            haskell_toolchain = haskell_toolchain,
            toolchain_deps = toolchain_deps,
            project_deps = project_deps,
            libs_tset = libs_tset,
            link_args = link_args,
        ),
        toolchain_lib_dyn_infos = toolchain_lib_dyn_infos,
        pkg_deps = pkg_deps,
    ))

    return [
        DefaultInfo(default_outputs = [lib]),
        HaskellLinkGroupInfo(
            pkgname = pkgname,
            db = db,
            lib = lib,
            libraries = hlibs,
        ),
    ]

def haskell_link_group_impl(ctx: AnalysisContext) -> list[Provider]:
    # for now
    link_style = LinkStyle("shared")
    enable_profiling = False

    registerer = ctx.attrs._ghc_pkg_registerer[RunInfo]
    haskell_toolchain = ctx.attrs._haskell_toolchain[HaskellToolchainInfo]
    linker_info = ctx.attrs._cxx_toolchain[CxxToolchainInfo].linker_info

    hlibs = [l.get(HaskellLibraryProvider).lib[link_style] for l in ctx.attrs.deps]
    direct_deps_info = [lib.info[link_style] for lib in attr_deps_haskell_link_infos(ctx)]

    # collect all the extra library dependencies from component Haskell libraries
    direct_extra_libs = [elib for lib in hlibs for elib in lib.extra_libraries]
    link_args = get_link_args_for_strategy(
        ctx,
        [
            lib[MergedLinkInfo]
            for lib in direct_extra_libs
            if MergedLinkInfo in lib
        ],
        to_link_strategy(link_style),
    )

    results = make_haskell_link_group(
        ctx.actions,
        label = ctx.label,
        hlibs = hlibs,
        direct_deps_info = direct_deps_info,
        link_style = link_style,
        enable_profiling = enable_profiling,
        registerer = registerer,
        haskell_toolchain = haskell_toolchain,
        linker_info = linker_info,
        link_args = link_args,
    )
    return results
